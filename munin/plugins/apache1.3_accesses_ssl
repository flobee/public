#!/usr/bin/perl
########################################################################
# apache accesses SSL for munin
# autor: Florian Blasel
# notes: taken from standard apache_* plugin for apache 1.3x
# $Id: apache_accesses_ssl.txt 419 2014-01-08 13:23:32Z flobee $
#########################################################################
#
# Plugin to monitor the number of accesses to Apache servers. It handles
# a list of ports passed in from a plugin configuration file.
#
# Requirements:
# 	- Needs access to https://localhost/server-status?auto (or modify the
# 	  address for another host). See your apache documentation on how to
# 	  set up this url in your httpd.conf. Apache needs ExtendedStatus
# 	  enabled for this plugin to work
#
# Tip: To see if it's already set up correctly, just run this plugin
# with the parameter "autoconf". If you get a "yes", everything should
# work like a charm already.
#
# Parameters supported:
#
# 	config
# 	autoconf
#
# Configurable variables
#
# 	url      - Override default status-url
# 	ports    - HTTP port numbers
#
# Magic markers:
#%# family=auto
#%# capabilities=autoconf
#
#
my $URL = "https://127.0.0.1/server-status?auto";
my @PORTS = exists $ENV{'ports'} ? split(' ', $ENV{'ports'}) : (443);
#
#
my $ret = undef;

if (! eval "require LWP::UserAgent;")
{
	$ret = "LWP::UserAgent not found";
}

if ( defined $ARGV[0] and $ARGV[0] eq "autoconf" )
{
	if ($ret)
	{
		print "no ($ret)\n";
		exit 1;
	}
	
	my $ua = LWP::UserAgent->new(timeout => 30);

	my @badports;
	foreach my $port (@PORTS) {
		my $response = $ua->request(HTTP::Request->new('GET',$URL));
		push @badports, $port unless $response->is_success and $response->content =~ /^Total Accesses:/im;
	}
	if (@badports) {
		print "no (no apache server-status or ExtendedStatus missing on ports @badports)\n";
		exit 1;
	} else {
		print "yes\n";
		exit 0;
	}
}

if ( defined $ARGV[0] and $ARGV[0] eq "config" )
{
	print "graph_title Apache SSL accesses\n";
	print "graph_args --base 1000\n";
	print "graph_vlabel accesses / \${graph_period}\n";
	print "graph_category apache\n";
	foreach my $port (@PORTS) {
		print "accesses$port.label port $port\n";
		print "accesses$port.type DERIVE\n";
		print "accesses$port.max 1000000\n";
		print "accesses$port.min 0\n";
	}
	exit 0;
}

my $ua = LWP::UserAgent->new(timeout => 30);

foreach my $port (@PORTS) {
	my $url = sprintf $URL, $port;
	my $response = $ua->request(HTTP::Request->new('GET',$url));
	if ($response->content =~ /^Total Accesses:\s+(.+)$/im) {
		print "accesses$port.value $1\n";
	} else {
		print "accesses$port.value U\n";
	}
}

# vim:syntax=perl
